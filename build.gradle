buildscript {
	repositories {
		maven { url 'https://repo.spring.io/milestone' }
		maven { url 'https://repo.spring.io/snapshot' }
		mavenCentral()
		gradlePluginPortal()
	}
	dependencies {
		classpath 'io.spring.javaformat:spring-javaformat-gradle-plugin:0.0.34'
	}

}

description = 'Micrometer Samples'

allprojects {
	group = 'com.example.micrometersamples'
	version = '0.0.1-SNAPSHOT'

	afterEvaluate { project -> println "I'm configuring $project.name with version $project.version" }
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.javaformat'
	apply plugin: 'maven-publish'

	java {
		registerFeature('optional') {
			usingSourceSet(sourceSets.main)
		}

		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	dependencies {
		// JSR-305 only used for non-required meta-annotations
		optionalApi 'com.google.code.findbugs:jsr305:latest.release'
	}

	test {
		// set heap size for the test JVM(s)
		maxHeapSize = "1500m"

		useJUnitPlatform()
	}

	project.tasks.withType(Test) { Test testTask ->
		testTask.testLogging.exceptionFormat = 'full'
	}

}

wrapper {
	gradleVersion = '7.5.1'
}

defaultTasks 'build'
