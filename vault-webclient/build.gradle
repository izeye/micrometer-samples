buildscript {
	repositories {
		maven { url 'https://repo.spring.io/milestone' }
		maven { url 'https://repo.spring.io/snapshot' }
		gradlePluginPortal()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
	}
}

apply plugin: 'org.springframework.boot'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
	implementation platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")

	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	implementation 'org.springframework.cloud:spring-cloud-starter-vault-config'

	implementation 'io.micrometer:context-propagation:1.0.0-SNAPSHOT' // TODO: Should come from Micrometer-Bom ?
	implementation "io.micrometer:micrometer-tracing-bridge-${project.hasProperty("brave") ? "brave" : "otel"}"
	// Default is OTel + Zipkin. You can opt in via "wavefront" to Wavefront and "brave" for Brave
	if (project.hasProperty("wavefront")) {
		implementation "io.micrometer:micrometer-tracing-reporter-wavefront"
	}
	if (project.hasProperty("brave")) {
		implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
	} else {
		implementation "io.opentelemetry:opentelemetry-exporter-zipkin"
	}

	testImplementation project(':test-common')
    testImplementation 'io.micrometer:micrometer-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:latest.release"
}
